WHITESPACE      = _{ " " | "\t" }

program         = _{ SOI ~ (stmt? ~ comment? ~ NEWLINE)* ~ stmt? ~ comment? ~ EOI }

comment         = @{ "//" ~ (LETTER | MARK | NUMBER | PUNCTUATION | SYMBOL | SPACE_SEPARATOR)* }

stmt            = _{ import_stmt | define_stmt | let_stmt }
import_stmt     = { "import" ~ ident }
define_stmt     = { "define" ~ ident ~ params ~ "=" ~ expr }
let_stmt        = { "let" ~ ident ~ "=" ~ expr }

expr            = _{ infix_expr | base_expr }
infix_expr      = { base_expr ~ (infix_op ~ base_expr)+ }
base_expr       = _{ prefix_expr | builtin_expr | call_expr | ident | literal | array | wrapped_expr }
prefix_expr     = { prefix_op ~ wrapped_expr }
builtin_expr    = { builtin ~ args }
call_expr       = { ident ~ args }
wrapped_expr    = _{ "(" ~ NEWLINE* ~ expr ~ NEWLINE* ~ ")" }

builtin         = _{ sum | product }

sum             = { "sum" }
product         = { "product" }

prefix_op       = _{ logical_not | abs | sqrt | sin | cos | tan | asin | acos | atan }

logical_not     = { "not" }
abs             = { "abs" }
sqrt            = { "sqrt" }
sin             = { "sin" }
cos             = { "cos" }
tan             = { "tan" }
asin            = { "asin" }
acos            = { "acos" }
atan            = { "atan" }

infix_op = _{
    plus | minus | multiply | divide | modulo | exponent |
    less_equal | less_than | greater_equal | greater_than | equal | not_equal |
    logical_and | logical_or
}

plus            = { "+" }
minus           = { "-" }
multiply        = { "*" }
divide          = { "/" }
modulo          = { "%" }
exponent        = { "^" }
less_than       = { "<" }
less_equal      = { "<=" }
greater_than    = { ">" }
greater_equal   = { ">=" }
equal           = { "==" }
not_equal       = { "!=" }
logical_and     = { "and" }
logical_or      = { "or" }

params          = { "(" ~ ident ~ ("," ~ ident)* ~ ")" }
args            = { "(" ~ expr ~ ("," ~ expr)* ~ ")" }

ident           = @{ (ASCII_ALPHA | "_") ~ (ASCII_ALPHA | "_")* }
literal         = @{ "-"? ~ ASCII_DIGIT+ ~ ("." ~ ASCII_DIGIT{1,3})? }
array           = { "[" ~ expr ~ ("," ~ expr)* ~ "]" }
